import com.google.gson.JsonParser

plugins {
    id 'com.android.application'
    id 'de.undercouch.download'
}

def getLatestFridaVersion = {
    def connection = new URL("https://api.github.com/repos/frida/frida/releases/latest").openConnection()

    connection.setRequestProperty("X-GitHub-Api-Version", "2022-11-28")
    connection.setRequestProperty("Accept", "application/vnd.github+json")

    def response = connection.inputStream.text
    def jsonObject = JsonParser.parseString(response).getAsJsonObject()

    return jsonObject.get("tag_name").getAsString()
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.rel.mujde"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    namespace 'com.rel.mujde'

    buildFeatures {
        buildConfig true
    }

    lint {
        abortOnError false
        checkReleaseBuilds false
        warningsAsErrors false
    }

    sourceSets {
        main {
            // import frida binaries from jniLibs directory
            jniLibs.srcDirs = ['src/main/jniLibs']
        }
    }

    configurations {
        downloader
        xzTools
    }

    dependencies {
        downloader 'de.undercouch:gradle-download-task:5.4.0'
        xzTools 'org.tukaani:xz:1.9'
    }

    task downloadFrida {
        doLast {
            // make xzTools importable
            URLClassLoader loader = new URLClassLoader(
                configurations.xzTools.files.collect { it.toURI().toURL() } as URL[],
                getClass().getClassLoader()
            )

            def jniLibsDir = new File(projectDir, 'src/main/jniLibs')
            def x86Dir = new File(jniLibsDir, 'x86')
            def x86_64Dir = new File(jniLibsDir, 'x86_64')
            def armDir = new File(jniLibsDir, 'armeabi-v7a')
            def arm64Dir = new File(jniLibsDir, 'arm64-v8a')

            [x86Dir, x86_64Dir, armDir, arm64Dir].each { it.mkdirs() }

            def injectorFileName = 'libfrida-inject.so'
            def fridaVersion = getLatestFridaVersion()

            def architectures = [
                'android-arm': new File(armDir, injectorFileName),
                'android-arm64': new File(arm64Dir, injectorFileName),
                'android-x86': new File(x86Dir, injectorFileName),
                'android-x86_64': new File(x86_64Dir, injectorFileName)
            ]

            architectures.each { arch, outputFile ->
                if (outputFile.exists()) {
                    println "frida for ${arch} already exists at ${outputFile.name}"
                    return
                }

                def tempDir = new File(buildDir, "frida-temp")
                tempDir.mkdirs()

                def injectorName = "frida-inject-${fridaVersion}-${arch}.xz"
                def compressedInjector = new File(tempDir, injectorName)
                def url = "https://github.com/frida/frida/releases/download/${fridaVersion}/${injectorName}"

                try {
                    download {
                        src url
                        dest compressedInjector
                        overwrite true
                    }

                    if (!compressedInjector.exists()) {
                        println "Failed to download: ${compressedInjector.absolutePath}"
                        return
                    }

                    def XZInputStream = loader.loadClass('org.tukaani.xz.XZInputStream')
                    def extractedFile = new File(compressedInjector.absolutePath.replace('.xz', ''))

                    compressedInjector.withInputStream { fis ->
                        def xzIn = XZInputStream.getConstructor(InputStream.class).newInstance(fis)
                        xzIn.withStream { xzis ->
                            extractedFile.withOutputStream { fos ->
                                fos << xzis
                            }
                        }
                    }

                    extractedFile.renameTo(outputFile)
                    println "Successfully extracted frida for ${arch}"

                } catch (Exception e) {
                    println "Error downloading or extracting ${arch}: ${e.message}"
                    e.printStackTrace()
                }
            }
        }
    }

    preBuild.dependsOn downloadFrida
}

repositories {
    google()
    mavenCentral()
    maven { url 'https://api.xposed.info/' }
}

dependencies {
    implementation 'androidx.core:core:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.cardview:cardview:1.0.0'

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.google.code.gson:gson:2.8.9'

    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly 'de.robv.android.xposed:api:82:sources'
}
